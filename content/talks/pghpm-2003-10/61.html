<html>
  <head><title>SPOPS is your new friend | Import price change history</title>
    <link rel="stylesheet" href="slide.css">
    <script language="JavaScript" src="slide.js"></script>
  </head>
<body>

<table width="100%" cellspacing="0" cellpadding="0">
   <tr bgcolor="#cccccc">
     <td width="33%" align="left"><a href="60.html" class="navlink" id="prevLink">&lt;&lt; Previous</a></td>
     <td width="34%" align="center"><a href="toc.html" class="navlink" id="tocLink">Contents</a></td>
     <td width="33%" align="right"><a href="62.html" class="navlink" id="nextLink">Next &gt;&gt;</a></td>
   </tr>
</table>

<h2>Import price change history</h2>

<div class="content">

<ul>
<li>Same as before, but add related objects as we go</li>
</ul>
<div class="code"><pre>
#!/usr/bin/perl

# assumes format:
#  publisher|title|issue|condition|price|writer|penciller|inker

use strict;
use SPOPS::Initialize;
use vars qw( $spops );

do 'comic_with_history_map.pl';
my $classes = SPOPS::Initialize-&gt;process({ config =&gt; $spops });
unless ( scalar @{ $classes } &gt; 0 ) {
    die "No classes processed!\n";
}

my ( $fail, $ok ) = (0,0);
while ( &lt;STDIN&gt; ) {
    chomp;
    next if ( /^\s*\#/ );
    next if ( /^\s*$/ );
    my ( $pub, $title, $num, $cond, $writer,
         $penciller, $inker, $notes, %all_price_info ) = split /\s*\|\s*/, $_;
    my ( $comic );
    eval {
        $comic = CBG::Comic-&gt;new(
                        { publisher =&gt; $pub,       title     =&gt; $title,
                          issue_num =&gt; $num,       condition =&gt; $cond,
                          writer    =&gt; $writer,    penciller =&gt; $penciller,
                          inker     =&gt; $inker,     notes     =&gt; $notes })
                  -&gt;save();
    };
    if ( $@ ) {
        warn "Failed to insert $title #$num: $@\n";
        $fail++;
    }
    else {
        $ok++;
        while ( my ( $date, $price ) = each %all_price_info ) {
            eval {
                CBG::ComicPrice-&gt;new({ comic_id    =&gt; $comic-&gt;id,
                                       change_date =&gt; $date,
                                       price       =&gt; $price })
                               -&gt;save()
            };
            if ( $@ ) {
                warn "Failed to insert price ($date: $price) for ($title: $num): $@";
            }
        }
    }
}

print "inserted ($ok); failed ($fail)\n";
</pre></div>

</div>
</body></html>